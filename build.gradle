plugins {
    id 'java'
}

subprojects {
    apply plugin: 'java'

    tasks.register('copyPluginYml', Copy) {
        from "$projectDir/src/main/resources/plugin.yml"
        into "$buildDir/resources/main"
    }

    jar {
        from rootProject.sourceSets.main.output
    }

    def targetJavaVersion = 21
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = targetJavaVersion
        targetCompatibility = targetJavaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release.set(targetJavaVersion)
        }
    }

    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }
}

allprojects {
    group = 'org.leralix'
    version = '0.11.0'

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        maven {
            name = "local"
            url = uri("${rootDir}/../TownsAndNations/build/repo")
        }
    }

    dependencies {
        compileOnly "org.spigotmc:spigot-api:1.20.2-R0.1-SNAPSHOT"
        compileOnly "io.github.leralix:sphere-lib:0.4.0"
            compileOnly "io.github.leralix:tan-api:0.2.0"
    }
}
